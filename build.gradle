plugins {
    id 'net.researchgate.release' version '3.0.0'
    id 'java-library'
    id  'maven-publish'
    id 'signing'
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

group = 'fr.maif'

ext {
    vavrVersion = "0.10.6"
    jacksonVersion = "2.18.4"
    lombokVersion = "1.18.38"
    jupiterVersion = "5.13.3"
}
repositories {
    mavenCentral()
}

java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    api "io.vavr:vavr:$vavrVersion"
    api "io.vavr:vavr-jackson:0.10.3"
    api "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    api "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    api "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    api "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion"
    api "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"

    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation(platform("org.junit:junit-bom:$jupiterVersion"))
    testImplementation("org.junit.jupiter:junit-jupiter")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")

    testImplementation "org.assertj:assertj-core:3.27.3"
    testImplementation "com.github.erosb:everit-json-schema:1.14.6"
}

tasks.named('test', Test) {
    useJUnitPlatform()
    testLogging {
        events "passed"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = "Functional Json"
                description = "Functional json parsing"
                inceptionYear = "2020"
                url = "https://github.com/maif/functional-json"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = 'alexandre.delegue'
                        name = 'Alexandre Del√®gue'
                        url = 'https://github.com/larousso'
                    }
                    developer {
                        id = 'benjamin.cavy'
                        name = 'Benjamin Cavy'
                        url = 'https://github.com/ptitFicus'
                    }
                }
                scm {
                    url = "https://github.com/maif/functional-json"
                    connection = "scm:git:git://github.com/maif/functional-json.git"
                    developerConnection = "scm:git:ssh://github.com/maif/functional-json.git"
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
            username = System.env.SONATYPE_USERNAME
            password = System.env.SONATYPE_PASSWORD
        }
    }
}

release {
    tagTemplate = 'v${version}'
}

release.git.requireBranch.set('master')